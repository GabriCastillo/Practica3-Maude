load parallel++.maude

mod DEKKER++ is
    inc PARALLEL++ .

    op crit : -> UserStatement .
    ops noCrit : -> LoopingUserStatement .
    ops code1 code2 : -> Program .
    op initial : -> MachineState .
    op wants-to-enter : -> BoolVar .
    op turn : -> IntVar .

    var  M : Memory .
    vars P R : Program .
    var  S : Soup . 
    var  I : Pid .

    eq code1 
      = repeat 
            wants-to-enter[0] := true ;
            while wants-to-enter[1] = true do
                if turn != 0 then
                    wants-to-enter[0] := false ;
                    while turn != 0 do
                        skip 
                    od ;
                    wants-to-enter[0] := true 
                else 
                    skip 
                fi 
            od ;
            crit ;
            turn := 1 ;
            wants-to-enter[0] := false ;
            noCrit 
        until true != true li .

    eq code2 
      = repeat 
            wants-to-enter[1] := true ;
            while wants-to-enter[0] = true do
                if turn != 1 then
                    wants-to-enter[1] := false ;
                    while turn != 1 do
                        skip 
                    od ;
                    wants-to-enter[1] := true 
                else 
                    skip 
                fi 
            od ;
            crit ;
            turn := 0 ;
            wants-to-enter[1] := false ;
            noCrit 
        until true != true li .

    eq initial = { [1, code1] | [2, code2], [turn, 1]} .
endm

eof

search initial =>! MS:MachineState .
search initial =>* {S | [1,crit ; R] | [2, crit ; P], M} .